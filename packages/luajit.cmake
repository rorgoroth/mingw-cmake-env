# luajit ships with a broken pkg-config file
configure_file(${CMAKE_CURRENT_SOURCE_DIR}/luajit.pc.in
               ${CMAKE_CURRENT_BINARY_DIR}/luajit.pc @ONLY)

ExternalProject_Add(
  luajit
  DEPENDS libiconv
  GIT_REPOSITORY https://github.com/Wohlsoft/LuaJIT.git
  GIT_REMOTE_NAME origin
  GIT_TAG v2.1
  UPDATE_COMMAND ""
  CONFIGURE_COMMAND
    ${EXEC} cmake -H<SOURCE_DIR> -B<BINARY_DIR> -G Ninja
    -DCMAKE_BUILD_TYPE=Release
    -DCMAKE_TOOLCHAIN_FILE=${TOOLCHAIN_FILE}
    -DCMAKE_INSTALL_PREFIX=${MINGW_INSTALL_PREFIX}
    -DCMAKE_FIND_ROOT_PATH=${MINGW_INSTALL_PREFIX}
    -DBUILD_SHARED_LIBS=OFF
    -DCMAKE_CROSSCOMPILING=ON
    -DCMAKE_UNITY_BUILD=ON
    -DUNITY_BUILD_BATCH_SIZE=0
    -DLUAJIT_BUILD_TOOL=OFF
    -DLJ_ENABLE_LARGEFILE=ON
    -DCMAKE_SYSTEM_VERSION=10
    -DLUAJIT_FORCE_UTF8_FOPEN=ON
    -DCMAKE_C_FLAGS='-DLUAJIT_ENABLE_LUA52COMPAT'
  BUILD_COMMAND ${NINJA} -C <BINARY_DIR>
  INSTALL_COMMAND ${NINJA} -C <BINARY_DIR> install
  LOG_DOWNLOAD 1
  LOG_UPDATE 1
  LOG_CONFIGURE 1
  LOG_BUILD 1
  LOG_INSTALL 1)

ExternalProject_Add_Step(
  luajit install-pc
  DEPENDEES install
  COMMAND
    ${CMAKE_COMMAND} -E copy
    ${CMAKE_CURRENT_BINARY_DIR}/luajit.pc
    ${MINGW_INSTALL_PREFIX}/lib/pkgconfig/luajit.pc)

force_rebuild_git(luajit)
